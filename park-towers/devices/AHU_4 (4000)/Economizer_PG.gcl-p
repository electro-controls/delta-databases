//Economizer sequence

//Economizer ramp delay is automatically longer during cold weather
'EconRampDly' = Scale ('Eff_OAT', 0, 20, 2000, 60, 60)


//Determine if free cooling can be achived by comparing OAT and RAT
'FreeCooling' = Switch ( 'FreeCooling', 'Eff_OAT', 'RAT' - 1, 'RAT' + 1)

//Economizer free cooling pct
If 'FreeCooling' And (Not 'DATMode') And ('Mode' = 1 Or 'Mode' = 4) Then //Occ or UnOcc Cooling
    'EconClgPct' = Scale ('DATClg_CO', 0, 0, 0, 100, 100)
  Else
    'EconClgPct' = 0
End If

If 'RunStatusInt' And ('Mode' = 1 Or 'Mode' = 4) Then //Occ or UnOcc Cooling
    'EconCalcMinOA' = Scale ('CO2_CO', 0, 0, 'MinOALimit', 100, 100)
    'Economizer' = (Max ('EconCalcMinOA', 'EconClgPct')) * ('EconRampPct' * 0.01) * ('MATLimit_CO' * 0.01)
Else
    'Economizer' = 0
    'EconCalcMinOA' = 0
End If

Variable MATInt

If ('MAT' < ('MATLimit_Sp' - 5)) OnFor 30S Then
    MATInt = Off
  Else
    MATInt = On
End If

If 'RunStatusInt' And MATInt And (('Mode' = 1) Or ('Mode' = 4)) Then //Occ or UnOcc Cooling
   DoEvery 10S
   'EconRampPct' = Min (100, 'EconRampPct' + (100 / 'EconRampDly' * 10))
   End Do
  Else
   'EconRampPct' = 0
End If