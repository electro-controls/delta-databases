//Disables hot water valve after a building purge sequence.
If 'ZoneOccMode' = 6 Then
    'ZonePurgeHold' = On
    'ZonePurgeHoldTmLeft' = 'ZonePurgeHold_Sp'
  Else
    DoEvery 1M
    'ZonePurgeHoldTmLeft' = Max (0, 'ZonePurgeHoldTmLeft' - 1)
    End Do
End If

If 'OAT_Eff' < 40 Or ('ZonePurgeHoldTmLeft' = 0) Then
    'ZonePurgeHold' = Off
End If

'HWVLock' = Switch ( 'HWVLock', 'Zone_EffClg', 99, 90)

//If (FanStatus = Off) And TerminalUnitFan Then //lock HWV if not fan not running
//    HWVLock = On
//End If

If 'HWVLock' Or 'ZonePurgeHold' Then
 'HWV_LocDemand' = 0
Else
 'HWV_LocDemand' = 'DATHtg_CO'
End If

If 'HWV_RemDemand' >= 0 Then
    'HWV_EffDemand' = 'HWV_RemDemand'
  Else
    'HWV_EffDemand' = 'HWV_LocDemand'
End If

//Three point floating valve
If PowerUp Then
  'HWVRecalc' = On
End If

If 'HWVRecalc' OnFor ('HWVRuntime_Sp' * 2):S Then
  'HWVRecalc' = Off
End If

If 'HWVAction' Then
    If 'HWVRecalc' Then
      'HWV_D1' = Off
      'HWV_D2' = On
      Else
      'HWV_D1' = Driveopen
      'HWV_D2' = Driveclose
    End If
    IfOnce 'HWV_D1' Then
      'HWVRuntimeDir' = On //Open
    End If
    IfOnce 'HWV_D2' Then
      'HWVRuntimeDir' = Off //Close
    End If
Else
    If 'HWVRecalc' Then
      'HWV_D1' = On
      'HWV_D2' = Off
      Else
      'HWV_D1' = Driveclose
      'HWV_D2' = Driveopen
    End If
    IfOnce 'HWV_D1' Then
      'HWVRuntimeDir' = Off //Close
    End If
    IfOnce 'HWV_D2' Then
      'HWVRuntimeDir' = On //Open
    End If
End If

//HWV is 3 point floating
Variable Driveopen
Variable Driveclose
Variable Min_Step As Real // Holds the minimum step for valve runtime

// Calculate the Minimum Step for the Valve Runtime
Min_Step = Truncate ((100 / 'HWVRuntime_Sp') + 1)

 If 'HWVAction' Then
      If 'HWV_D1' Then
        DoEvery 1S
        'HWVRuntime' = Min ('HWVRuntime' + 1, 'HWVRuntime_Sp')
        End Do
      End If
      If 'HWV_D2' Then
        DoEvery 1S
        'HWVRuntime' = Max ('HWVRuntime' - 1, 0)
        End Do
      End If
  Else
      If 'HWV_D2' Then
        DoEvery 1S
        'HWVRuntime' = Min ('HWVRuntime' + 1, 'HWVRuntime_Sp')
        End Do
      End If
      If 'HWV_D1' Then
        DoEvery 1S
        'HWVRuntime' = Max ('HWVRuntime' - 1, 0)
        End Do
      End If
End If

//Actual Valve Operation
DoEvery 1S
'HWVCalcPct' = Scale ('HWVRuntime', 0, 0, 0, 'HWVRuntime_Sp', 100)
If ('HWVCalcPct' > ('HWV_EffDemand' + Min_Step)) Then
      Driveclose = On
      Driveopen = Off
     Else If ('HWVCalcPct' < ('HWV_EffDemand' - Min_Step)) Then
      Driveopen = On
      Driveclose = Off
     Else If 'HWV_EffDemand' = 0 Then
      DoEvery 30S
      Driveclose = On
      Driveopen = Off
      End Do
      If Driveclose OnFor 2S Then
        Driveclose = Off
        Driveopen = Off
      End If
     Else If 'HWV_EffDemand' = 100 Then
      DoEvery 30S
      Driveclose = Off
      Driveopen = On
      End Do
      If Driveopen OnFor 2S Then
        Driveclose = Off
        Driveopen = Off
      End If
    Else
      Driveclose = Off
      Driveopen = Off
    End If
    End If
    End If
End If
End Do