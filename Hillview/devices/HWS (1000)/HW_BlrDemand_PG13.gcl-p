'HW_BlrDemand_PG13.Write_Priority' = 13

Variable localHeatCall

If ('Snowmelt_Heat_Call' Or 'Pool_Heat_Call' Or 'Spa_Heat_Call') OnFor 10S Then
  localHeatCall = On
Else
  localHeatCall = Off
End If

//System enable
If ('HW_HWSystemMode' = 1) Then
    If 'HW_Rem_BldgHeatCall' Or ('HW_OATInt1' And 'HW_Rem_BldgOccMWUCall') Or 'HW_OATInt2' Or localHeatCall Then
      'HW_SystemEnable' = On
    Else
      'HW_SystemEnable' = Off
    End If
  ElseIf  ('HW_HWSystemMode' = 2) Then
      'HW_SystemEnable' = On
  Else
      'HW_SystemEnable' = Off
End If
If 'HW_SystemEnable' OffFor 60S Then
    'HW_HWS_CO.Bias' = 1
    'HW_SystemRamp' = 0
End If

// oat interlocks
If ('OAT_Eff' < 'HW_OATInt1_Sp') Then
    DoEvery 3M
    'HW_OATInt1TmLeft' = Max (0, 'HW_OATInt1TmLeft' - 0.05)
    End Do
  Else
    'HW_OATInt1TmLeft' = 'HW_OATInt1Delay_Sp'
End If

If 'HW_OATInt1TmLeft' <= 0 Then
    'HW_OATInt1' = On
  Else
    'HW_OATInt1' = Off
End If

If (('OAT_Eff' < 'HW_OATInt2_Sp') And (Not 'HW_OATInt2')) OnFor 30M Then
    'HW_OATInt2' = On
End If
If (('OAT_Eff' > 'HW_OATInt2_Sp') And 'HW_OATInt2') OnFor 30M Then
    'HW_OATInt2' = Off
End If

//Ramp delay. If the system is warm, we dont need as long of a ramp.
Variable RampMult
RampMult = Scale ('HW_HWS', 0, 110, 1, 160, 0.2)

If 'HW_SystemEnable' Then
    DoEvery 10S
      'HW_SystemRamp' = Min (100, 'HW_SystemRamp' + (100 / ('HW_SystemRamp_Sp' * RampMult * 6)))
    End Do
End If
IfOnce 'HW_HWHighLimit' Then
    'HW_SystemRamp' = 'HW_SystemRamp' * 0.25
End If

//Allows for remote manual override of control loop Demand
If 'HW_HWSDemand_Rem' >= 0 Then
    'HW_HWSDemand_Eff' = 'HW_HWSDemand_Rem'
  Else
    'HW_HWSDemand_Eff' = 'HW_HWSDemand_Loc'
End If

//Calculated HWs Demand percent.
'HW_HWSDemand_Loc' = 'HW_HWS_CO' * 'HW_SystemRamp' * 0.01

//boiler call
//below is applicable for one boiler only
If 'HW_SystemEnable' Then
    'HW_B1Call' = On
  Else
    'HW_B1Call' = Off
End If