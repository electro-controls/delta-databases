//Fan Program

//Fan start/stop sequence
If ('ZoneOccMode' <> 1) Then
    'SFEnable' = On
  Else
    'SFEnable' = Off
End If

'SFStatus' = Switch ( 'SFStatus', 'SFCurrent', 0.2, 0.05)

//Running status interlock sequence. Used by
//other routines (economizer, VFD speed, etc..)
If 'SFEnable' And 'SFStatus' Then
    'RunStatusInt' = On
  Else
    'RunStatusInt' = Off
End If


Variable MAUSpeed
Variable CoolingSpeed
Variable HeatingSpeed
Variable SpeedDemand


MAUSpeed = Scale ('AV11150', 0, 'SFCalcAirflowAtMinSpeed_Sp', 'SFMinSpeed_Sp', 'SFCalcAirflowAtMaxSpeed_Sp', 'SFMaxSpeed_Sp')
CoolingSpeed = Scale ('Zone_EffClg', 0, 50, 'SFMinSpeed_Sp', 100, 'SFMaxSpeed_Sp')
HeatingSpeed = Scale ('Zone_EffHtg', 0, 80, 'SFMinSpeed_Sp', 100, 'SFMaxSpeed_Sp')
SpeedDemand = Max (MAUSpeed, CoolingSpeed, HeatingSpeed)


//VFD speed sequence. Insures smooth start up via
//the ramp routine.
//Fan speed reset on zone heat and cooling demand


If 'RunStatusInt' Then
    DoEvery 10S
    'SFRampResult' = Min (100, 'SFRampResult' + (1000 / 'SFRamp_Sp'))
    End Do
  Else
    'SFRampResult' = 0
End If

If PowerUp Then
  'SFRampResult' = 0
End If

If ('OACalcOffset_CO' > 50) And ('SpaceStatic_CO' > 50) Then
    'DX1Enable' = On
End If

If ('OACalcOffset_CO' < 10) And ('DX1Enable' OnFor 5M) Then
    'DX1Enable' = Off
End If

If 'DX1Enable' Then
    'AO10' = Min ('AO9', Scale ('OACalcOffset_CO', 0, 50, 0, 100, 'SpaceStatic_CO'))
Else
    'AO10' = 0
End If