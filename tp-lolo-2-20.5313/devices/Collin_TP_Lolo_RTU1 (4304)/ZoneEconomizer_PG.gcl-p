//Economizer sequence
//Economizer ramp dalay is automatically longer during
//cold weather.
'AV11210' = Scale ('OAT_Eff', 0, 20, 2000, 60, 60)

If 'RunStatusInt' And (Not 'MATWarning') And (('ZoneOccMode' = 3) Or ('ZoneOccMode' = 4)) Then //Occ/Bypass
   DoEvery 10S
   'AV11211' = Min (100, 'AV11211' + (100 / 'AV11210' * 10))
   End Do
  Else
   'AV11211' = 0
End If

If ('AO7.Priority_Array[8]' <= 100) Then //Look for manual override at Pri 8
    'AV11211' = Min ('AV11211', 'AO7') //This puts ramp at overridden value so it doesnt trip freeze after a release to auto
End If
    
IfOnce ('ZoneControlMode' = 2) Then //Cooling
    'AV11211' = Min ('AV11211', Scale ('OAT_Eff', 0, 0, 25, 60, 75)) //Prevents overshoot when coming into cooling mode
End If

If ('MAT_Eff' < ('AV11242' - 5)) OnFor 30S Then
    'MATWarning' = On
  Else
    'MATWarning' = Off
End If

'AV11241' = 'MATLimit_CO'

//Determines if free cooling can be achived by comparing
// OAT and RAT (or Zone temp)
'BV11220' = Switch ( 'BV11220', 'OAT_Eff', 'RAT_Eff' - 1, 'RAT_Eff' + 1)


If 'AV11222' >= 0 Then
    'AV11220' = 'AV11222'
  Else
    'AV11220' = 'AV11221'
End If

If 'BV11220' And ('ZoneOccMode' = 3 Or 'ZoneOccMode' = 4) And ('ZoneControlMode' = 2) And 'Dx1_OAInt' Then //Occ/Bypass and Cooling
    'AV11221' = Scale ('DATClg_CO', 0, 0, 0, 50, 100)
  ElseIf  'BV11220' And ('ZoneOccMode' = 3 Or 'ZoneOccMode' = 4) And ('ZoneControlMode' = 2) Then
    'AV11221' = Scale ('DATClg_CO', 0, 0, 0, 100, 100)
  Else
    'AV11221' = 0
End If


If 'AV11232' >= 0 Then
    'AV11230' = 'AV11232'
  Else
    'AV11230' = 'AV11231'
End If

'AV11231' = Max ('AV11235', Scale (Min ('Zone_CO2Limit_Sp', 'ZoneCO2_EffDemand'), 0, 0, 'AV11233', 100, 'AV11257'))

If 'OAT_Eff' > 70 Then
    'AV11257' = Scale ('OAT_Eff', 0, 80, 100, 100, 50) //limit demand in hot weather
  Else
    'AV11257' = Scale ('OAT_Eff', 0, 0, 50, 55, 100) //limit demand in cold weather
End If

If 'RunStatusInt' And ('ZoneOccMode' = 3 Or 'ZoneOccMode' = 4) Then //Occ/Bypass
    'AO7' = (Scale ('AV11220', 0, 0, 'AV11230', 100, 100)) * ('AV11241' * 0.01) * ('AV11211' * 0.01) //(Max (MinOAPct, EconClgPct)) * (EconRampPct * 0.01) * (MATLimit_CO * 0.01)
Else
    'AO7' = 0
End If