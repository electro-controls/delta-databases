
//DX mechanical cooling sequence

Variable DATDemand As Integer
Variable ClgStg2Off As Integer

If 'BO6' OffFor 3M Then //This ensure previous stage is off before sequence off another stage
  ClgStg2Off = On
Else
  ClgStg2Off = Off
End If


If 'Dx_FlowInt' And (('SFOntimeRef' > 20) Or (Not 'BV11220')) And ('DATClg_CO' >= 90) And ('DAT' > ('DAT_EffSp' + 3)) OnFor 300S Then
    DATDemand = On
End If
If (Not 'Dx_FlowInt') Or ('DAT' <= ('DAT_EffSp' + 1)) Then //turns off latch near setpoint
    DATDemand = Off
End If

If ('Zone_EffClg' >= 90) And 'Dx1_OAInt' And DATDemand And ((Not 'Dx1_Demand') OnFor 300S) Then
    'Dx1_Demand' = On
End If

If (('DATClg_CO' < 1) And ('DAT' < ('DAT_EffSp' - 5)) And ClgStg2Off) OnFor 10M Then //turns off if DAT overshoots
    'Dx1_Demand' = Off
End If

If (ClgStg2Off And ('Zone_EffClg' < 20)) Then //turns off on decrease of zone cooling
    'Dx1_Demand' = Off
End If

If (('DAT' < 40) And ClgStg2Off) OnFor 300S Then //prevent frost on coil due to low DAT
    'Dx1_Demand' = Off
End If

If (('DATClg_CO' < 1) And ClgStg2Off) OnFor 60M Then //Turns off if DAT demand is met for a long duration
    'Dx1_Demand' = Off
End If

If (Not 'Dx1_OAInt') Then //Kick off on OATInt
    'Dx1_Demand' = Off
End If

IfOnce (Not 'BO5') Then //This ensures it will start back with delays in case of manual override or non typical inactive
    'Dx1_Demand' = Off
End If

If ('DAT_EffSp' > ('OAT_Eff' - 2)) And (((Not 'Dx_Demand') And 'Dx1_Demand') OnFor 30M) Then //This should bounce it back to economizer for a try
    'Dx1_Demand' = Off
End If


//DX2 Interlocks
If ('DATClg_CO' >= 99) And ('Zone_EffClg' >= 99) And 'BV11252' And DATDemand And ('BO5' OnFor 10M) Then
    'BV11262' = On
End If

If (('DATClg_CO' < 30) And ('Zone_EffClg' < 60)) OnFor 5M Then
    'BV11262' = Off
End If

If ('Zone_EffClg' < 50) Then
    'BV11262' = Off
End If

If (('DAT' < 40) OnFor 120S) Then //Critical shut off to avoid frost on evap
    'BV11262' = Off
End If

If Not 'BV11252' Then
    'BV11262' = Off
End If

IfOnce Not 'BO6' Then
    'BV11262' = Off
End If

If ('DAT_EffSp' > ('OAT_Eff' - 2)) And ('BV11262' OnFor 30M) Then //Reverts back to try economizer
    'BV11262' = Off
End If



//Below locks out DX cooling if unoccupied and not unoc cooling call without free cooling
If ('ZoneOccMode' <> 3) And ('ZoneOccMode' <> 4) And (Not 'ZoneUnocClCall') And (Not 'BV11220') Then
    'Dx1_Demand' = Off
    'BV11262' = Off
End If