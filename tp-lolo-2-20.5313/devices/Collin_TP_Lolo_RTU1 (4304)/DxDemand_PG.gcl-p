
//DX mechanical cooling sequence

Variable DATDemand As Integer

If 'Dx_FlowInt' And ('SFOntimeRef' > 20) And ('DATClg_CO' >= 90) And ('DAT' > ('DAT_EffSp' + 3)) OnFor 300S Then
    DATDemand = On
End If
If (Not 'Dx_FlowInt') Or ('DAT' <= ('DAT_EffSp' + 1)) Then //turns off latch near setpoint
    DATDemand = Off
End If

If ('Zone_EffClg' >= 90) And 'Dx1_OAInt' And DATDemand And ((Not 'Dx1_Demand') OnFor 300S) Then
    'Dx1_Demand' = On
End If

If ('DATClg_CO' < 1) And ('DAT' < ('DAT_EffSp' - 5)) OnFor 10M Then //turns off if DAT overshoots
    'Dx1_Demand' = Off
End If

If ('Zone_EffClg' < 20) Then //turns off on decrease of zone cooling
    'Dx1_Demand' = Off
End If

If ('DAT' < 40) OnFor 300S Then //prevent frost on coil due to low DAT
    'Dx1_Demand' = Off
End If

If ('DATClg_CO' < 1) OnFor 60M Then //Turns off if DAT demand is met for a long duration
    'Dx1_Demand' = Off
End If

If (Not 'Dx1_OAInt') Then //Kick off on OATInt
    'Dx1_Demand' = Off
End If

IfOnce (Not 'DX1Enable') Then //This ensures it will start back with delays in case of manual override or non typical inactive
    'Dx1_Demand' = Off
End If

If (((Not 'Dx_Demand') And 'Dx1_Demand') OnFor 30M) Then //This should bounce it back to economizer for a try
    'Dx1_Demand' = Off
End If

//Below locks out DX cooling if unoccupied and not unoc cooling call without free cooling
If ('ZoneOccMode' <> 3) And ('ZoneOccMode' <> 4) And (Not 'ZoneUnocClCall') Then
    'Dx1_Demand' = Off
End If