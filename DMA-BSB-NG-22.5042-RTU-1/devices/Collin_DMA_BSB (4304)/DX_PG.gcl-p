Variable DATClgDemand
If ('DATClg_CO' < 1) OnFor 10M Then
    DATClgDemand = Off
End If

If ('DATClg_CO' > 5) And ('DAT' > 'DATClg_Sp') Then
    DATClgDemand = On
End If

//DX mechanical cooling sequence

//Outdoor Air Interlocks
'Dx1_OAInt' = Switch ( 'Dx1_OAInt', 'Eff_OAT', 'DX1OAInt_Sp', 'DX1OAInt_Sp' - 3)

//DX air Flow Interlocks

If 'RunStatusInt' Then
    'DX_FlowInt' = Switch ( 'DX_FlowInt', 'DuctStaticPressure', ('DuctStatic_Sp' * 0.2), ('DuctStatic_Sp' * 0.1))
  Else
    'DX_FlowInt' = Off
End If

Variable DATDemand
Variable DX1Demand
Variable DXEnable

If 'DX_FlowInt' Then
    DATDemand = Switch ( DATDemand, 'DAT', 'DATClg_Sp' + 5, 'DATClg_Sp')
  Else
    DATDemand = 0
End If

If ((('DATClg_CO' >= 70) And 'Dx1_OAInt' And DATDemand) OnFor 120S) Then
    DX1Demand = On
End If

If ((('DATClg_CO' < 5) And ('DAT' < ('DATClg_Sp' - 5))) OnFor 15M) Then
    DX1Demand = Off
End If

If (('DAT' < 43) OnFor 60S) Then
    DX1Demand = Off
End If

If (('DATClg_CO' < 1)) OnFor 60M Then
    DX1Demand = Off
End If
If Not 'Dx1_OAInt' Then
    DX1Demand = Off
End If

IfOnce Not DXEnable Then
    DX1Demand = Off
End If
If 'DATClg_Sp' > ('Eff_OAT' - 2) Then
    DX1Demand = Off
End If

//DX1 Interlocks
If DX1Demand And 'Dx1_OAInt' And 'DX_FlowInt' Then
    DXEnable = On
  Else
    DXEnable = Off
End If

IfOnce DXEnable Then
 'DATClg_CO.Bias' = 10
End If

If DXEnable Then
  'DXCapacity' = Scale ('DATClg_CO', 0, 10, 10, 100, 100)
Else
  'DXCapacity' = 0
End If