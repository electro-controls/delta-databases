//Fan Program

//Fan start/stop sequence
If 'Mode' <> 5 Then //Off
    'SFVFDEnable' = On
  Else
    'SFVFDEnable' = Off
End If

//Running status interlock sequence. Used by
//other routines (economizer, VFD speed, etc..)
'SFStatus' = Switch ( 'SFStatus', 'SFRunStatus', 1.5, 0.25)
'ReliefFanStatus' = Switch ( 'ReliefFanStatus', 'RF_EffCurrent', 0.05, 0.02)

If 'RunStatusInt' Then
  DoEvery 1M
    'SFOntimeRef' = 'SFOntimeRef' + 1
  End Do
Else
  'SFOntimeRef' = 0
End If

If 'RFVFDEnable' Then
 'RF_EffCurrent' = 'RFRunStatus'
Else
 'RF_EffCurrent' = 0
End If



If 'SFVFDEnable' And 'SFStatus' Then
    'RunStatusInt' = On
  Else
    'RunStatusInt' = Off
End If


//VFD speed sequence. Insures smooth start up via
//the ramp routine.
'SFVFDSpeedRef' = ('VFDRampResult' * 0.01) * 'DuctStatic_CO'

If 'RunStatusInt' Then
  DoEvery 10S
  'VFDRampResult' = Min (100, 'VFDRampResult' + (1000 / 'VSDRamp_Sp'))
  End Do
Else
  'VFDRampResult' = 0
End If

If PowerUp Then
'VFDRampResult' = 0
End If

//Balancer supplied a max Relief of 84%
If 'EffBldgStaticPress' > ('BldgPressureSp' + 0.005) Then
  'RFVFDEnable' = Switch ( 'RFVFDEnable', 'EconDamper', 30, 28)
ElseIf  'EffBldgStaticPress' <= 'BldgPressureSp' Then
  'RFVFDEnable' = Off
End If


If 'RFVFDEnable' Then
  //RFVFDSpeedRef = Scale (EconDamper, 0, 30, 10, 100, rfset)
  'RFVFDSpeedRef' = Min ('BldgPressure_CO', 84)
Else
  'RFVFDSpeedRef' = 0
End If

//Variable rfset
//rfset = Min (84, (BldgPressure_CO * SFVFDSpeedRef * 0.01))

 //EF enable
 If 'EF_Enable' Then
  'EF1_Speed' = 'EF_SpeedSp'
 Else
 'EF1_Speed' = 0
 End If

 'EF_Status' = Switch ( 'EF_Status', 'EF1_Current', 0.3, 0.15)


//End of Program