//eZNS pg1
If PowerUp Then
  'LinkLCD1.Line_1' = 'Eff_OAT'
  'LinkLCD1.Line_2' = 'CUH3_Eff_ZoneTemp'
  'LinkLCD1.Line_3' = 'CUH3_Zone_EffSp'
  'LinkLCD1.Line_1_Units' = 1
  'LinkLCD1.Line_2_Units' = 3
  'LinkLCD1.Line_3_Units' = 1
  'LinkLCD1.Occupancy' = 2
  'LinkLCD1.On' = 1
  'CUH3_DNSAlgMode' = 1
  'CUH3_BypassTm_Sp' = 2
End If

//Local setpoint adjust enable
//Change mode requires that button 2 be pressed 4 times (or as programmed) 
//prior to being able to change stat setting. This eleviates unauthorized tampering.
IfOnce 'CUH3_DNSMode' = 1 Then
    'CUH3_EnableDNS' = On
    'LinkLCD1.Set' = 0
End If

IfOnce 'CUH3_DNSMode' = 2 Then
    'CUH3_EnableDNS' = Off
    'LinkLCD1.Set' = 0
End If

If 'CUH3_DNSMode' = 3 Then
  Variable KeyCount
  IfOnce ('LinkLCD1.Key_Press' = 2) Then
    KeyCount = KeyCount + 1
  End If
  If KeyCount >= 4 Then
      'CUH3_EnableDNS' = On
  End If
  If ('LinkLCD1.Key_Press' = 0) OnFor 60S Then
      'CUH3_EnableDNS' = Off
      KeyCount = 0
  End If
  IfOnce 'CUH3_EnableDNS' Then
    'LinkLCD1.Set' = 1
  End If
  IfOnce Not 'CUH3_EnableDNS' Then
    'LinkLCD1.Set' = 0
  End If
End If
IfOnce 'CUH3_DNSMode' = 2 Then
    'CUH3_EnableDNS' = Off
    'LinkLCD1.Set' = 0
End If

//This updates the display temps on a periodic basis
DoEvery 1M
  'LinkLCD1.Line_1' = 'Eff_OAT'
  'LinkLCD1.Line_2' = 'CUH3_Eff_ZoneTemp'
  'LinkLCD1.Line_3' = 'CUH3_Zone_EffSp'
  'LinkLCD1.Line_3_Units' = 1
End Do

//Occupancy Icons
IfOnce ('CUH3_OccMode' = 3) Or ('CUH3_OccMode' = 4) Then
    'LinkLCD1.Occupancy' = 2
    'LinkLCD1.On' = 1
    'LinkLCD1.Off' = 0
End If

IfOnce ('CUH3_OccMode' = 1) Or ('CUH3_OccMode' = 2) Then
    'LinkLCD1.Occupancy' = 1
    'LinkLCD1.On' = 0
    'LinkLCD1.Off' = 1
End If

//Heat Icon
IfOnce ('CUH3_EffZoneHtg' >= 5) Then
    'LinkLCD1.Heating' = 1
End If
IfOnce ('CUH3_EffZoneHtg' <= 1) Then
    'LinkLCD1.Heating' = 0
End If

//Cool Icon
IfOnce ('CUH3_EffZoneClg' >= 5) Then
    'LinkLCD1.Cooling' = 1
End If
IfOnce ('CUH3_EffZoneClg' <= 1) Then
    'LinkLCD1.Cooling' = 0
End If

//If unit is in alarm the stat will blink
IfOnce 'CUH3_BlinkDNS' Then
    'LinkLCD1.Blink' = 1
End If
IfOnce Not 'CUH3_BlinkDNS' Then
    'LinkLCD1.Blink' = 0
End If

//end of eZNS pg1