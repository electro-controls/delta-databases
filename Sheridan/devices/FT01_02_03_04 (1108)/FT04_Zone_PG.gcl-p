//Zone_PG
Variable HtCall
Variable ClCall
Variable HtRamp
Variable ClRamp

//Eff SP
If ('FT04_OccMode' = 3) Or ('FT04_OccMode' = 4) Or ('FT04_OccMode' = 5) Then //occ, bypass, warmup
    'FT04_ZoneHtg_EffSp' = Min (('FT04_Zone_EffSp' - ('FT04_ZoneDb_Sp' * 0.5)), 'FT04_OcHtgMax_Sp')
    'FT04_ZoneClg_EffSp' = Max (('FT04_Zone_EffSp' + ('FT04_ZoneDb_Sp' * 0.5)), 'FT04_OcClgMin_Sp')
  ElseIf  'FT04_OccMode' = 2 Then //standby
    'FT04_ZoneHtg_EffSp' = Min (('FT04_Zone_EffSp' - 'FT04_StandbyOffset_Sp' - ('FT04_ZoneDb_Sp' * 0.5)), 'FT04_OcHtgMax_Sp')
    'FT04_ZoneClg_EffSp' = Max (('FT04_Zone_EffSp' + 'FT04_StandbyOffset_Sp' + ('FT04_ZoneDb_Sp' * 0.5)), 'FT04_OcClgMin_Sp')
  Else //unocc
    'FT04_ZoneHtg_EffSp' = Min ('FT04_UnocHt_Sp', 'FT04_OcHtgMax_Sp')
    'FT04_ZoneClg_EffSp' = Max ('FT04_UnocCl_Sp', 'FT04_OcClgMin_Sp')
End If

//allow remote override of zone sp
If 'FT04_Zone_RemSp' > 0 Then
    'FT04_Zone_EffSp' = 'FT04_Zone_RemSp'
  Else
    'FT04_Zone_EffSp' = 'FT04_Zone_LocSp'
End If

//calculate local demand after ramp
'FT04_LocZoneHtg' = 'FT04_ZoneHtg_CO' * HtRamp * 0.01
'FT04_LocZoneClg' = 'FT04_ZoneClg_CO' * ClRamp * 0.01

//heating or cooling call and previous mode latching
If (('FT04_ZoneHtg_CO' > ('FT04_EffZoneClg' + 5)) And 'FT04_ControlMode' = 3) OnFor 'FT04_ZoneDbDelay_Sp':S Then
    HtCall = On
    'FT04_PrevControlMode' = 3
  ElseIf  (('FT04_ZoneClg_CO' > ('FT04_EffZoneHtg' + 5)) And 'FT04_ControlMode' = 3) OnFor 'FT04_ZoneDbDelay_Sp':S Then
    ClCall = On
    'FT04_PrevControlMode' = 3
  ElseIf  (('FT04_ControlMode' = 1) And ('FT04_EffZoneHtg' <= 0)) OnFor 'FT04_ZoneDbDelay_Sp':S Then
    HtCall = Off
    'FT04_PrevControlMode' = 1
  ElseIf  (('FT04_ControlMode' = 2) And ('FT04_EffZoneClg' <= 0)) OnFor 'FT04_ZoneDbDelay_Sp':S Then
    ClCall = Off
      'FT04_PrevControlMode' = 2
End If

//determine control mode
If 'FT04_RemZoneHtg' >= 0 Then //remote htg
    'FT04_EffZoneHtg' = 'FT04_RemZoneHtg'
    'FT04_EffZoneClg' = 0
    'FT04_ControlMode' = 1
  ElseIf  'FT04_RemZoneClg' >= 0 Then //remote clg
    'FT04_EffZoneClg' = 'FT04_RemZoneClg'
    'FT04_EffZoneHtg' = 0
    'FT04_ControlMode' = 2
  ElseIf  ClCall Then //auto clg
    'FT04_ControlMode' = 2
    'FT04_EffZoneHtg' = 'FT04_LocZoneHtg'
    'FT04_EffZoneClg' = 'FT04_LocZoneClg'
  ElseIf  HtCall Then
    'FT04_ControlMode' = 1 //auto htg
    'FT04_EffZoneHtg' = 'FT04_LocZoneHtg'
    'FT04_EffZoneClg' = 'FT04_LocZoneClg'
  Else
    'FT04_ControlMode' = 3 //auto deadband
    'FT04_EffZoneHtg' = 'FT04_LocZoneHtg'
    'FT04_EffZoneClg' = 'FT04_LocZoneClg'
End If

//Ramp delay
If 'FT04_ControlMode' = 1 Then
    DoEvery 10S
    HtRamp = Min (100, HtRamp + (1000 / 'FT04_ZoneHtRamp_Sp'))
    End Do
  Else
    HtRamp = 0
End If
If 'FT04_ControlMode' = 2 Then
    DoEvery 10S
    ClRamp = Min (100, ClRamp + (1000 / 'FT04_ZoneClRamp_Sp'))
    End Do
  Else
    ClRamp = 0
End If

//unocc htg demand & standby deleted bc they weren't used

//End of Program