//Zone_PG
Variable HtCall
Variable ClCall
Variable HtRamp
Variable ClRamp

//Eff SP
If ('FT01_OccMode' = 3) Or ('FT01_OccMode' = 4) Or ('FT01_OccMode' = 5) Then //occ, bypass, warmup
    'FT01_ZoneHtg_EffSp' = Min (('FT01_Zone_EffSp' - ('FT01_ZoneDb_Sp' * 0.5)), 'FT01_OcHtgMax_Sp')
    'FT01_ZoneClg_EffSp' = Max (('FT01_Zone_EffSp' + ('FT01_ZoneDb_Sp' * 0.5)), 'FT01_OcClgMin_Sp')
  ElseIf  'FT01_OccMode' = 2 Then //standby
    'FT01_ZoneHtg_EffSp' = Min (('FT01_Zone_EffSp' - 'FT01_StandbyOffset_Sp' - ('FT01_ZoneDb_Sp' * 0.5)), 'FT01_OcHtgMax_Sp')
    'FT01_ZoneClg_EffSp' = Max (('FT01_Zone_EffSp' + 'FT01_StandbyOffset_Sp' + ('FT01_ZoneDb_Sp' * 0.5)), 'FT01_OcClgMin_Sp')
  Else //unocc
    'FT01_ZoneHtg_EffSp' = Min ('FT01_UnocHt_Sp', 'FT01_OcHtgMax_Sp')
    'FT01_ZoneClg_EffSp' = Max ('FT01_UnocCl_Sp', 'FT01_OcClgMin_Sp')
End If

//allow remote override of zone sp
If 'FT01_Zone_RemSp' > 0 Then
    'FT01_Zone_EffSp' = 'FT01_Zone_RemSp'
  Else
    'FT01_Zone_EffSp' = 'FT01_Zone_LocSp'
End If

//calculate local demand after ramp
'FT01_LocZoneHtg' = 'FT01_ZoneHtg_CO' * HtRamp * 0.01
'FT01_LocZoneClg' = 'FT01_ZoneClg_CO' * ClRamp * 0.01

//heating or cooling call and previous mode latching
If (('FT01_ZoneHtg_CO' > ('FT01_EffZoneClg' + 5)) And 'FT01_ControlMode' = 3) OnFor 'FT01_ZoneDbDelay_Sp':S Then
    HtCall = On
    'FT01_PrevControlMode' = 3
  ElseIf  (('FT01_ZoneClg_CO' > ('FT01_EffZoneHtg' + 5)) And 'FT01_ControlMode' = 3) OnFor 'FT01_ZoneDbDelay_Sp':S Then
    ClCall = On
    'FT01_PrevControlMode' = 3
  ElseIf  (('FT01_ControlMode' = 1) And ('FT01_EffZoneHtg' <= 0)) OnFor 'FT01_ZoneDbDelay_Sp':S Then
    HtCall = Off
    'FT01_PrevControlMode' = 1
  ElseIf  (('FT01_ControlMode' = 2) And ('FT01_EffZoneClg' <= 0)) OnFor 'FT01_ZoneDbDelay_Sp':S Then
    ClCall = Off
      'FT01_PrevControlMode' = 2
End If

//determine control mode
If 'FT01_RemZoneHtg' >= 0 Then //remote htg
    'FT01_EffZoneHtg' = 'FT01_RemZoneHtg'
    'FT01_EffZoneClg' = 0
    'FT01_ControlMode' = 1
  ElseIf  'FT01_RemZoneClg' >= 0 Then //remote clg
    'FT01_EffZoneClg' = 'FT01_RemZoneClg'
    'FT01_EffZoneHtg' = 0
    'FT01_ControlMode' = 2
  ElseIf  ClCall Then //auto clg
    'FT01_ControlMode' = 2
    'FT01_EffZoneHtg' = 'FT01_LocZoneHtg'
    'FT01_EffZoneClg' = 'FT01_LocZoneClg'
  ElseIf  HtCall Then
    'FT01_ControlMode' = 1 //auto htg
    'FT01_EffZoneHtg' = 'FT01_LocZoneHtg'
    'FT01_EffZoneClg' = 'FT01_LocZoneClg'
  Else
    'FT01_ControlMode' = 3 //auto deadband
    'FT01_EffZoneHtg' = 'FT01_LocZoneHtg'
    'FT01_EffZoneClg' = 'FT01_LocZoneClg'
End If

//Ramp delay
If 'FT01_ControlMode' = 1 Then
    DoEvery 10S
    HtRamp = Min (100, HtRamp + (1000 / 'FT01_ZoneHtRamp_Sp'))
    End Do
  Else
    HtRamp = 0
End If
If 'FT01_ControlMode' = 2 Then
    DoEvery 10S
    ClRamp = Min (100, ClRamp + (1000 / 'FT01_ZoneClRamp_Sp'))
    End Do
  Else
    ClRamp = 0
End If

//unocc htg demand & standby deleted bc they weren't used

//End of Program