//Zone_PG
Variable HtCall
Variable ClCall
Variable HtRamp
Variable ClRamp

//Eff SP
If ('FT02_OccMode' = 3) Or ('FT02_OccMode' = 4) Or ('FT02_OccMode' = 5) Then //occ, bypass, warmup
    'FT02_ZoneHtg_EffSp' = Min (('FT02_Zone_EffSp' - ('FT02_ZoneDb_Sp' * 0.5)), 'FT02_OcHtgMax_Sp')
    'FT02_ZoneClg_EffSp' = Max (('FT02_Zone_EffSp' + ('FT02_ZoneDb_Sp' * 0.5)), 'FT02_OcClgMin_Sp')
  ElseIf  'FT02_OccMode' = 2 Then //standby
    'FT02_ZoneHtg_EffSp' = Min (('FT02_Zone_EffSp' - 'FT02_StandbyOffset_Sp' - ('FT02_ZoneDb_Sp' * 0.5)), 'FT02_OcHtgMax_Sp')
    'FT02_ZoneClg_EffSp' = Max (('FT02_Zone_EffSp' + 'FT02_StandbyOffset_Sp' + ('FT02_ZoneDb_Sp' * 0.5)), 'FT02_OcClgMin_Sp')
  Else //unocc
    'FT02_ZoneHtg_EffSp' = Min ('FT02_UnocHt_Sp', 'FT02_OcHtgMax_Sp')
    'FT02_ZoneClg_EffSp' = Max ('FT02_UnocCl_Sp', 'FT02_OcClgMin_Sp')
End If

//allow remote override of zone sp
If 'FT02_Zone_RemSp' > 0 Then
    'FT02_Zone_EffSp' = 'FT02_Zone_RemSp'
  Else
    'FT02_Zone_EffSp' = 'FT02_Zone_LocSp'
End If

//calculate local demand after ramp
'FT02_LocZoneHtg' = 'FT02_ZoneHtg_CO' * HtRamp * 0.01
'FT02_LocZoneClg' = 'FT02_ZoneClg_CO' * ClRamp * 0.01

//heating or cooling call and previous mode latching
If (('FT02_ZoneHtg_CO' > ('FT02_EffZoneClg' + 5)) And 'FT02_ControlMode' = 3) OnFor 'FT02_ZoneDbDelay_Sp':S Then
    HtCall = On
    'FT02_PrevControlMode' = 3
  ElseIf  (('FT02_ZoneClg_CO' > ('FT02_EffZoneHtg' + 5)) And 'FT02_ControlMode' = 3) OnFor 'FT02_ZoneDbDelay_Sp':S Then
    ClCall = On
    'FT02_PrevControlMode' = 3
  ElseIf  (('FT02_ControlMode' = 1) And ('FT02_EffZoneHtg' <= 0)) OnFor 'FT02_ZoneDbDelay_Sp':S Then
    HtCall = Off
    'FT02_PrevControlMode' = 1
  ElseIf  (('FT02_ControlMode' = 2) And ('FT02_EffZoneClg' <= 0)) OnFor 'FT02_ZoneDbDelay_Sp':S Then
    ClCall = Off
      'FT02_PrevControlMode' = 2
End If

//determine control mode
If 'FT02_RemZoneHtg' >= 0 Then //remote htg
    'FT02_EffZoneHtg' = 'FT02_RemZoneHtg'
    'FT02_EffZoneClg' = 0
    'FT02_ControlMode' = 1
  ElseIf  'FT02_RemZoneClg' >= 0 Then //remote clg
    'FT02_EffZoneClg' = 'FT02_RemZoneClg'
    'FT02_EffZoneHtg' = 0
    'FT02_ControlMode' = 2
  ElseIf  ClCall Then //auto clg
    'FT02_ControlMode' = 2
    'FT02_EffZoneHtg' = 'FT02_LocZoneHtg'
    'FT02_EffZoneClg' = 'FT02_LocZoneClg'
  ElseIf  HtCall Then
    'FT02_ControlMode' = 1 //auto htg
    'FT02_EffZoneHtg' = 'FT02_LocZoneHtg'
    'FT02_EffZoneClg' = 'FT02_LocZoneClg'
  Else
    'FT02_ControlMode' = 3 //auto deadband
    'FT02_EffZoneHtg' = 'FT02_LocZoneHtg'
    'FT02_EffZoneClg' = 'FT02_LocZoneClg'
End If

//Ramp delay
If 'FT02_ControlMode' = 1 Then
    DoEvery 10S
    HtRamp = Min (100, HtRamp + (1000 / 'FT02_ZoneHtRamp_Sp'))
    End Do
  Else
    HtRamp = 0
End If
If 'FT02_ControlMode' = 2 Then
    DoEvery 10S
    ClRamp = Min (100, ClRamp + (1000 / 'FT02_ZoneClRamp_Sp'))
    End Do
  Else
    ClRamp = 0
End If

//unocc htg demand & standby deleted bc they weren't used

//End of Program