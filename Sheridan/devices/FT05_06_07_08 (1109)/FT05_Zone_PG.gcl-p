//Zone_PG
Variable HtCall
Variable ClCall
Variable HtRamp
Variable ClRamp

//Eff SP
If ('FT05_OccMode' = 3) Or ('FT05_OccMode' = 4) Or ('FT05_OccMode' = 5) Then //occ, bypass, warmup
    'FT05_ZoneHtg_EffSp' = Min (('FT05_Zone_EffSp' - ('FT05_ZoneDb_Sp' * 0.5)), 'FT05_OcHtgMax_Sp')
    'FT05_ZoneClg_EffSp' = Max (('FT05_Zone_EffSp' + ('FT05_ZoneDb_Sp' * 0.5)), 'FT05_OcClgMin_Sp')
  ElseIf  'FT05_OccMode' = 2 Then //standby
    'FT05_ZoneHtg_EffSp' = Min (('FT05_Zone_EffSp' - 'FT05_StandbyOffset_Sp' - ('FT05_ZoneDb_Sp' * 0.5)), 'FT05_OcHtgMax_Sp')
    'FT05_ZoneClg_EffSp' = Max (('FT05_Zone_EffSp' + 'FT05_StandbyOffset_Sp' + ('FT05_ZoneDb_Sp' * 0.5)), 'FT05_OcClgMin_Sp')
  Else //unocc
    'FT05_ZoneHtg_EffSp' = Min ('FT05_UnocHt_Sp', 'FT05_OcHtgMax_Sp')
    'FT05_ZoneClg_EffSp' = Max ('FT05_UnocCl_Sp', 'FT05_OcClgMin_Sp')
End If

//allow remote override of zone sp
If 'FT05_Zone_RemSp' > 0 Then
    'FT05_Zone_EffSp' = 'FT05_Zone_RemSp'
  Else
    'FT05_Zone_EffSp' = 'FT05_Zone_LocSp'
End If

//calculate local demand after ramp
'FT05_LocZoneHtg' = 'FT05_ZoneHtg_CO' * HtRamp * 0.01
'FT05_LocZoneClg' = 'FT05_ZoneClg_CO' * ClRamp * 0.01

//heating or cooling call and previous mode latching
If (('FT05_ZoneHtg_CO' > ('FT05_EffZoneClg' + 5)) And 'FT05_ControlMode' = 3) OnFor 'FT05_ZoneDbDelay_Sp':S Then
    HtCall = On
    'FT05_PrevControlMode' = 3
  ElseIf  (('FT05_ZoneClg_CO' > ('FT05_EffZoneHtg' + 5)) And 'FT05_ControlMode' = 3) OnFor 'FT05_ZoneDbDelay_Sp':S Then
    ClCall = On
    'FT05_PrevControlMode' = 3
  ElseIf  (('FT05_ControlMode' = 1) And ('FT05_EffZoneHtg' <= 0)) OnFor 'FT05_ZoneDbDelay_Sp':S Then
    HtCall = Off
    'FT05_PrevControlMode' = 1
  ElseIf  (('FT05_ControlMode' = 2) And ('FT05_EffZoneClg' <= 0)) OnFor 'FT05_ZoneDbDelay_Sp':S Then
    ClCall = Off
      'FT05_PrevControlMode' = 2
End If

//determine control mode
If 'FT05_RemZoneHtg' >= 0 Then //remote htg
    'FT05_EffZoneHtg' = 'FT05_RemZoneHtg'
    'FT05_EffZoneClg' = 0
    'FT05_ControlMode' = 1
  ElseIf  'FT05_RemZoneClg' >= 0 Then //remote clg
    'FT05_EffZoneClg' = 'FT05_RemZoneClg'
    'FT05_EffZoneHtg' = 0
    'FT05_ControlMode' = 2
  ElseIf  ClCall Then //auto clg
    'FT05_ControlMode' = 2
    'FT05_EffZoneHtg' = 'FT05_LocZoneHtg'
    'FT05_EffZoneClg' = 'FT05_LocZoneClg'
  ElseIf  HtCall Then
    'FT05_ControlMode' = 1 //auto htg
    'FT05_EffZoneHtg' = 'FT05_LocZoneHtg'
    'FT05_EffZoneClg' = 'FT05_LocZoneClg'
  Else
    'FT05_ControlMode' = 3 //auto deadband
    'FT05_EffZoneHtg' = 'FT05_LocZoneHtg'
    'FT05_EffZoneClg' = 'FT05_LocZoneClg'
End If

//Ramp delay
If 'FT05_ControlMode' = 1 Then
    DoEvery 10S
    HtRamp = Min (100, HtRamp + (1000 / 'FT05_ZoneHtRamp_Sp'))
    End Do
  Else
    HtRamp = 0
End If
If 'FT05_ControlMode' = 2 Then
    DoEvery 10S
    ClRamp = Min (100, ClRamp + (1000 / 'FT05_ZoneClRamp_Sp'))
    End Do
  Else
    ClRamp = 0
End If

//unocc htg demand & standby deleted bc they weren't used

//End of Program