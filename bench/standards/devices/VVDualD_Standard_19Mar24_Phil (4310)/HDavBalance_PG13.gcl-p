'HDavBalance_PG13.Write_Priority' = 13

//If actual airflow entered into TABMeasurment (AV..181) calibrate K-Factor. 
IfOnce ('HDav_TABMeasurment' > 0) And (('HDavAlarm' < 3) OnFor 2S) And ('HDavZeroCountCal' > 1) And ('HDavCurrentCount' > ('HDavZeroCountCal' + 10)) Then
    'HDavAlarm' = 8 //Calibration in Progress
End If
IfOnce ('HDav_TABMeasurment' > 0) And ('HDavAlarm' = 7) Then
    'HDav_TABMeasurment.Out_Of_Service' = FALSE //Releases hand to disallow calibration if zero cal has not been done first
End If


IfOnce ('HDav_TABMeasurment' > 0) And ('HDavCurrentCount' < ('HDavZeroCountCal' + 10)) Then
    'HDavAlarm' = 6 //Informs TAB that there is a problem with low airflow 
End If
IfOnce ('HDavAlarm' = 6) And ('HDavCurrentCount' > ('HDavZeroCountCal' + 20)) Then
    'HDavAlarm' = 1 //Reverts status back to normal if airflow is established
End If


Variable KFacAtt
IfOnce ('HDavAlarm' = 8) Then
   'HDavCalOffset.Out_Of_Service' = FALSE //Clears override if point is in manual
   KFacAtt = ('HDav_TABMeasurment' / 'HDav_EffAirflow') * ('HDavKFactor' + 'HDavCalOffset')
End If

If KFacAtt Changed Then
 'HDavCalOffset' = KFacAtt - 'HDavKFactor' //adjusts value of cal offset
End If

//If calibrating from Orcaview the hand flag is thrown so we know we need to write a fallback to TABMeasurement
If ('HDavAlarm' > 2) And ('HDav_TABMeasurment.Out_Of_Service' = TRUE) And ('HDav_TABMeasurment' > 10) Then //Done calibrating from Orcaview
  'HDav_TABMeasurment.Out_Of_Service' = FALSE //Releases hand condition 
  'HDavAlarm' = 1 //Returns to normal
End If
IfOnce ('HDav_TABMeasurment.Out_Of_Service' = FALSE) OnFor 2S Then
  'HDav_TABMeasurment' = - 2 //Places default value
End If

//If TAB Measurement is coming from Tridium, then we wait for tridium to write a -1
IfOnce 'HDav_TABMeasurment' = - 1 Then //Done calibrating from Tridium
  'HDav_TABMeasurment.Out_Of_Service' = FALSE //Releases hand condition if applicable
  'HDavAlarm' = 1 //Returns to normal
End If


// Airflow Error & Error Pct
'HDavError' = 'HDav_EffAirflow' - 'HDav_EffSp'
'HDavErrorPct' = Min (('HDav_EffAirflow' / 'HDav_EffSp') * 100, 500)


//The below sequece sets the auto zero sequence and calibrates
//the sensor zero value
If ('HDavZeroCountCal' < 10) Or 'HDavAutoZeroCal' Then
    'HDavAlarm' = 7 //Zero Count alert message
End If
IfOnce (Not 'HDavAutoZeroCal') And ('HDavZeroCountCal' > 10) Then
    'HDavAlarm' = 1 //Releases status back to Standing by after a zero cal
End If

IfOnce 'HDavAutoZeroCal' Then
    'HDavZeroCountCal.Out_Of_Service' = FALSE //Clears manual hand flag
    'HDavAutoZeroCal.Out_Of_Service' = FALSE //Clears manual hand flag   
End If

IfOnce 'HDavAutoZeroCal' OnFor ('HDavDmpRuntime_Sp' * 2):S Then //Allows time for damper to close
    'HDavZeroCountCal' = 'HDavCurrentCount' //Capture the zero count value
    'HDavAutoZeroCal' = Off
End If



//The following saves the controller to flash so that TAB and Zero Cal values are saved.
IfOnce ('HDavCalOffset' Changed) Then
//  SaveToFlash
  'LastAutoSavetoFlash' = 0
End If
IfOnce ('HDavZeroCountCal' Changed) Then //Save zero count value to flash memory
//  SaveToFlash
  'LastAutoSavetoFlash' = 0
End If