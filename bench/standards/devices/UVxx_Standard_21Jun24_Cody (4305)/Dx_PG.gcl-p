'Dx_PG.Write_Priority' = 13
////The following variables are placeholders for the actual Dx outputs
////Perform a search and replace and remove these placeholders if Dx outputs are in play
Variable DxStage1_BOx
Variable DxStage2_BOx
////End of placeholder section

If 'FanStatus' Then //Add other airflow proving methodes when available (Ex. Filter Dp, Airflow, duct static, etc)
    'Dx_FlowInt' = On
  Else
    'Dx_FlowInt' = Off
End If
////NOTE: make sure to put Dx_FlowInt on Critical_PG as well!

If ('OAT_Eff' > ('Dx1OAInt_Sp' + 0.25)) And ('Dx1_OAInt' OffFor 15M) Then
    'Dx1_OAInt' = On
End If
If ('OAT_Eff' < ('Dx1OAInt_Sp' - 0.25)) And ('Dx1_OAInt' OnFor 15M) Then
    'Dx1_OAInt' = Off
End If

If ('OAT_Eff' > ('Dx2OAInt_Sp' + 0.25)) And ('Dx2_OAInt' OffFor 15M) Then
    'Dx2_OAInt' = On
End If
If ('OAT_Eff' < ('Dx2OAInt_Sp' - 0.25)) And ('Dx2_OAInt' OnFor 15M) Then
    'Dx2_OAInt' = Off
End If


Variable Dx1StartPct

If 'EconFreeClg' Then
    Dx1StartPct = 75
  Else
    Dx1StartPct = 50
End If

If ('Zone_EffClg' > Dx1StartPct) And 'Dx1_OAInt' And 'Dx_FlowInt' And ('DAT_Eff' > ('DAT_EffSp' + 3)) And (('EconRamp' > 99) OnFor 10M) Then
   'Dx1_Demand' = On
End If

If ('Zone_EffClg' < (Dx1StartPct - 25)) Or (Not 'Dx1_OAInt') Or (Not 'Dx_FlowInt') Or (('DAT_Eff' < 45) OnFor 5M) Then
   'Dx1_Demand' = Off
End If

If ('Zone_EffClg' > 99) And 'Dx2_OAInt' And 'Dx_FlowInt' And ('DAT_Eff' > ('DAT_EffSp' + 3)) And ('Dx1_Demand' OnFor 10M) Then
   'Dx2_Demand' = On
End If

If ('Zone_EffClg' < 75) Or (Not 'Dx2_OAInt') Or (Not 'Dx_FlowInt') Or (('DAT_Eff' < 48) OnFor 5M) Then
   'Dx2_Demand' = Off
End If

If 'Dx1_Demand' Then
    DxStage1_BOx = On
  Else
    DxStage1_BOx = Off
End If
If 'Dx2_Demand' Then
    DxStage2_BOx = On
  Else
    DxStage2_BOx = Off
End If