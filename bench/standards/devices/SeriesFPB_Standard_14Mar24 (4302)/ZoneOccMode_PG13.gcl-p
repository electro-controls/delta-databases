'ZoneOccMode_PG13.Write_Priority' = 13
//Set the occ mode. 
If 'ZoneAlgMode' = 1 And ('Zone_RemSchedule' Or 'ZoneMotionCall') Then
    'ZoneOccMode' = 3 //Occupied
  ElseIf  'ZoneBypassCall' Then
    'ZoneOccMode' = 4 //Bypass
  ElseIf  'Zone_RemPurgeCall' Then
    'ZoneOccMode' = 6 //Purge
  ElseIf  'Zone_RemWarmCall' Then
    'ZoneOccMode' = 5 //Warmup
  ElseIf  'ZoneAlgMode' = 0 And (Not 'Zone_RemSchedule') And (Not 'ZoneMotionCall') And (Not 'Zone_SchedueFunction') Then
    'ZoneOccMode' = 1 //Unocc
  Else
    'ZoneOccMode' = 2 //Standby
End If

IfOnce 'Zone_RemSchedule' Or 'ZoneMotionCall' Then
    'ZoneAlgMode' = 1
End If

IfOnce (Not 'Zone_RemSchedule') And (Not 'ZoneBypassCall') And (Not 'ZoneMotionCall') Then
    'ZoneAlgMode' = 0
End If


If 'Motion' OnFor 10S Then
    'ZoneMotionTmLeft' = 'ZoneMotionDelay_Sp'
  Else
    DoEvery 1M
    'ZoneMotionTmLeft' = Max (0, 'ZoneMotionTmLeft' - 1)
    End Do
End If

If 'ZoneMotionTmLeft' > 0 Then
    'ZoneMotionCall' = On
  Else
    'ZoneMotionCall' = Off
End If


//The following sequence allows the Stanbyby mode to revert back to occupied after
//a time delay. This is good for building where the ventilation should revert back without someone pressing the 
//on (1) buttom. Example would be class room, where teach can turn unit off (standby) but it could revert back to occupied
//after a time period (like 30-60Minutes). Another example would be an office enviroment where you dont not want standby at all
//in which you could put the standby time to 0.
IfOnce ('ZoneOccMode' = 2) Then
    'ZoneStandbyTmLeft' = 'ZoneStandbyDelay_Sp'
End If

DoEvery 30S
  'ZoneStandbyTmLeft' = Max (0, 'ZoneStandbyTmLeft' - 0.5)
End Do

If ('ZoneOccMode' = 2) And ('ZoneStandbyTmLeft' = 0) And ('Zone_RemSchedule' Or 'ZoneMotionCall') Then
    'ZoneAlgMode' = 1
End If