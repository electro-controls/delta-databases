'ZoneeZNSDisplay1_PG13.Write_Priority' = 13
If PowerUp Then
  'LCD101.Line_2' = 'Zone_EffTemp'
  'LCD101.Line_2_Units' = 1
  'LCD101.Line_3' = 'Zone_EffSp'
  'LCD101.Line_3_Units' = 1
  'LCD101.Occupancy' = 2
  'ZoneAlgMode' = 1
End If

//Local setpoint adjust enable
//Change mode requires that button 2 be pressed 4 times (or as programmed) 
//prior to being able to change stat setting. This eleviates unauthorized tampering.
IfOnce 'ZoneDNSMode' = 1 Then
    'ZoneEnableDNS' = On
    'LCD101.Set' = 0
End If

IfOnce 'ZoneDNSMode' = 2 Then
    'ZoneEnableDNS' = Off
    'LCD101.Set' = 0
End If

If 'ZoneDNSMode' = 3 Then
  Variable KeyCount
  IfOnce ('LCD101.Key_Press' = 2) Then
    KeyCount = KeyCount + 1
  End If
  If KeyCount >= 4 Then
      'ZoneEnableDNS' = On
  End If
  If ('LCD101.Key_Press' = 0) OnFor 60S Then
      'ZoneEnableDNS' = Off
      KeyCount = 0
  End If
  IfOnce 'ZoneEnableDNS' Then
    'LCD101.Set' = 1
  End If
  IfOnce Not 'ZoneEnableDNS' Then
    'LCD101.Set' = 0
  End If
End If
IfOnce 'ZoneDNSMode' = 2 Then
    'ZoneEnableDNS' = Off
    'LCD101.Set' = 0
End If

//This updates the display temps on a periodic basis
DoEvery 1M
  'LCD101.Line_2' = 'Zone_EffTemp'
  'LCD101.Line_2_Units' = 1
  'LCD101.Line_3' = 'Zone_EffSp'
  'LCD101.Line_3_Units' = 1
End Do

//Occupancy Icons
IfOnce 'ZoneOccMode' > 2 Then
    'LCD101.Occupancy' = 2
    'LCD101.On' = 1
    'LCD101.Off' = 0
End If

IfOnce 'ZoneOccMode' < 3 Then
    'LCD101.Occupancy' = 1
    'LCD101.On' = 0
    'LCD101.Off' = 1
End If

//Heat Icon
'LCD101.Heating' = Switch ( 'LCD101.Heating', 'Zone_EffHtg', 5, 1)

//Cool Icon
'LCD101.Cooling' = Switch ( 'LCD101.Cooling', 'Zone_EffClg', 5, 1)

//If unit is in alarm the stat will blink
'LCD101.Blink' = 'ZoneBlinkDNS'