//Pump sequence continued from PG6...

//enable, status, speed, ramps, failure, & flow interlock


//enable pumps 

If 'HWP1_Call' Then
    'HWP1_Enable' = On
End If

If ('HWP1_Ramp' < 1) And (Not 'HWP1_Call') Then
    'HWP1_Enable' = Off
End If

If 'HWP2_Call' Then
    'HWP2_Enable' = On
End If

If ('HWP2_Ramp' < 1) And (Not 'HWP2_Call') Then
    'HWP2_Enable' = Off
End If

If 'SystemEnable' OffFor 2S Then //this is kind of redundant.  calls should be off from PG6,=then 5m;to ramp down 
    'HWP1_Enable' = Off
    'HWP2_Enable' = Off
End If

//pump status using CTs. confirm amp thresholds
'HWP1_Status' = Switch ( 'HWP1_Status', 'HWP1_Current', 2, 1)
'HWP2_Status' = Switch ( 'HWP2_Status', 'HWP2_Current', 2, 1)

//no pump speed control
//modulate bypass valve
If 'HWP1_Enable' Or 'HWP2_Enable' Or 'SystemFlow' Then
    'HW_BypassValve' = Max ('HWDp_CO', Scale (MaxRamp, 0, 0, 100, 100, 0))
  Else
    'HW_BypassValve' = 100 //open
    'HWDp_CO.Bias' = 100
End If


//ramps
Variable MaxRamp
MaxRamp = Max ('HWP1_Ramp', 'HWP2_Ramp')

If 'HWP1_Call' And 'HWP1_Status' Then
    DoEvery 10S
      'HWP1_Ramp' = Min (100, 'HWP1_Ramp' + (100 / ('PmpRamp_Sp' * 6))) //takes 2 min to ramp up from 0 to 100
    End Do
  Else
    DoEvery 10S
      'HWP1_Ramp' = Max (0, 'HWP1_Ramp' - (100 / ('PmpRamp_Sp' * 15))) //takes 5min to ramp down from 100 to 0
    End Do
End If

If 'HWP2_Call' And 'HWP2_Status' Then
    DoEvery 10S
      'HWP2_Ramp' = Min (100, 'HWP2_Ramp' + (100 / ('PmpRamp_Sp' * 6))) //takes 2 min to ramp up from 0 to 100
    End Do
  Else
    DoEvery 10S
      'HWP2_Ramp' = Max (0, 'HWP2_Ramp' - (100 / ('PmpRamp_Sp' * 15))) //takes 5min to ramp down from 100 to 0
    End Do
End If


//pump failure
If ('HWP1_Enable' And (Not 'HWP1_Status')) OnFor 'PumpFailDly_Sp':S Then
    'HWP1_Fail' = On
  Else
    'HWP1_Fail' = Off
End If

If ('HWP2_Enable' And (Not 'HWP2_Status')) OnFor 'PumpFailDly_Sp':S Then
    'HWP2_Fail' = On
  Else
    'HWP2_Fail' = Off
End If


//flow interlock
Variable DpFlow
DpFlow = Switch ( DpFlow, 'HWDp', ('HWDp_Sp' * 0.15), ('HWDp_Sp' * 0.1))

If ('HWP1_Status' Or 'HWP2_Status') And DpFlow Then
    'SystemFlow' = On
  Else
    'SystemFlow' = Off
End If

//If Not SystemFlow Then //not used because this would be for controlling pump speed
//  HWDp_CO.Bias = 1
//End If

//end of pump sequence