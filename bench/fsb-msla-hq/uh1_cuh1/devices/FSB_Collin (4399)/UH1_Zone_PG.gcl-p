//The following sequnce detrmines the current mode of
//operation (Heating, Cooling, or Deadband)
Variable HtCall
Variable ClCall
Variable HtRamp
Variable ClRamp
Variable LastState

// Effective SP
If ('UH5_OccMode' = 3) Or ('UH5_OccMode' = 4) Or ('UH5_OccMode' = 5) Then
    'UH5_ZoneHeat_EffSp' = Min (('UH5_Zone_EffSp' - ('UH5_ZoneDb_Sp' * 0.5)), 'UH5_OcHtgMax_Sp')
    'UH5_ZoneCool_EffSp' = Max (('UH5_Zone_EffSp' + ('UH5_ZoneDb_Sp' * 0.5)), 'UH5_OcClgMin_Sp')
  ElseIf  'UH5_OccMode' = 2 Then //Standby
    'UH5_ZoneHeat_EffSp' = Min (('UH5_Zone_EffSp' - 'UH5_StandbyOffset_Sp' - ('UH5_ZoneDb_Sp' * 0.5)), 'UH5_OcHtgMax_Sp')
    'UH5_ZoneCool_EffSp' = Max (('UH5_Zone_EffSp' + 'UH5_StandbyOffset_Sp' + ('UH5_ZoneDb_Sp' * 0.5)), 'UH5_OcClgMin_Sp')
  ElseIf  'UH5_OccMode' = 6 Then //Purge
    'UH5_ZoneHeat_EffSp' = Min (('UH5_Zone_EffSp' - 'UH5_StandbyOffset_Sp' - ('UH5_ZoneDb_Sp' * 0.5)), 'UH5_OcHtgMax_Sp')
    'UH5_ZoneCool_EffSp' = Max (('UH5_Zone_EffSp' + ('UH5_ZoneDb_Sp' * 0.5)), 'UH5_OcClgMin_Sp')
  Else
    'UH5_ZoneHeat_EffSp' = Min ('UH5_UnocHt_Sp', 'UH5_OcHtgMax_Sp')
    'UH5_ZoneCool_EffSp' = Max ('UH5_UnocCl_Sp', 'UH5_OcClgMin_Sp')
End If

//Links remote setpoint to effective
If 'UH5_Zone_RemSp' > 0 Then
 'UH5_Zone_EffSp' = 'UH5_Zone_RemSp'
Else
 'UH5_Zone_EffSp' = 'UH5_Zone_LocSp'
End If

//This just display the value of the CO for display at the NM level
'UH5_LocZoneHtg' = 'UH5_ZoneHtg_CO' * HtRamp * 0.01
'UH5_LocZoneClg' = 'UH5_ZoneClg_CO' * ClRamp * 0.01

//This determines the actual heating/cooling/deadband mode
//This also does the previous mode latching. Previous mode could then be used
//by other routines (like DAT control).
If (('UH5_ZoneHtg_CO' > 'UH5_EffZoneClg' + 5) And 'UH5_ControlMode' = 3) OnFor 'UH5_ZoneDbDelay_Sp':S Then
    HtCall = On
    'UH5_PrevControlMode' = 3
  ElseIf  (('UH5_ZoneClg_CO' > 'UH5_EffZoneHtg' + 5) And 'UH5_ControlMode' = 3) OnFor 'UH5_ZoneDbDelay_Sp':S Then
    ClCall = On
    'UH5_PrevControlMode' = 3
  ElseIf  (('UH5_ControlMode' = 1) And ('UH5_EffZoneHtg' <= 0)) OnFor 'UH5_ZoneDbDelay_Sp':S Then
    HtCall = Off
    'UH5_PrevControlMode' = 1
  ElseIf  (('UH5_ControlMode' = 2) And ('UH5_EffZoneClg' <= 0)) OnFor 'UH5_ZoneDbDelay_Sp':S Then
    ClCall = Off
    'UH5_PrevControlMode' = 2
End If

//Set current control mode
//This takes into account a manual override of the space heating/cooling %
//from the NM
If 'UH5_RemZoneHtg' >= 0 Then
    'UH5_EffZoneHtg' = 'UH5_RemZoneHtg'
    'UH5_EffZoneClg' = 0
    'UH5_ControlMode' = 1
  ElseIf  'UH5_RemZoneClg' >= 0 Then
    'UH5_EffZoneClg' = 'UH5_RemZoneClg'
    'UH5_EffZoneHtg' = 0
    'UH5_ControlMode' = 2
  ElseIf  ClCall Then
    'UH5_ControlMode' = 2
    'UH5_EffZoneHtg' = 'UH5_LocZoneHtg'
    'UH5_EffZoneClg' = 'UH5_LocZoneClg'
  ElseIf  HtCall Then
    'UH5_ControlMode' = 1
    'UH5_EffZoneHtg' = 'UH5_LocZoneHtg'
    'UH5_EffZoneClg' = 'UH5_LocZoneClg'
  Else
    'UH5_ControlMode' = 3
    'UH5_EffZoneHtg' = 'UH5_LocZoneHtg'
    'UH5_EffZoneClg' = 'UH5_LocZoneClg'
End If

//This provides ramp delay to sequence in mode %. Intended
//to minimize overshoot of cooresponding cascade control loops when
//major swings are seen as setpoints and/or zone temps. Also, allows for
//smoother sequenceing of staged outputs (when applicable).
If 'UH5_ControlMode' = 1 Then
    DoEvery 10S
    HtRamp = Min (100, HtRamp + (1000 / 'UH5_ZoneHtRamp_Sp'))
    End Do
  Else
    HtRamp = 0
End If
If 'UH5_ControlMode' = 2 Then
    DoEvery 10S
    ClRamp = Min (100, ClRamp + (1000 / 'UH5_ZoneClRamp_Sp'))
    End Do
  Else
    ClRamp = 0
End If

//Unoc Demand
'UH5_UnocHtCall' = Switch ( 'UH5_UnocHtCall', 'UH5_Eff_ZoneTemp', 'UH5_UnocHt_Sp', 'UH5_UnocHt_Sp' + 'UH5_UnocDb_Sp')
'UH5_UnocClCall' = Switch ( 'UH5_UnocClCall', 'UH5_Eff_ZoneTemp', 'UH5_UnocCl_Sp', 'UH5_UnocCl_Sp' - 'UH5_UnocDb_Sp')

//Standby Demand
'UH5_ZoneClDemand' = Switch ( 'UH5_ZoneClDemand', 'UH5_EffZoneClg', 99, 50)
'UH5_ZoneHtDemand' = Switch ( 'UH5_ZoneHtDemand', 'UH5_EffZoneHtg', 99, 50)