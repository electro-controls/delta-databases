//Pumps Program


//Pump Failure
If ('HWP1Enable' < 'HWPLagStop_Sp') And ('HWPLagAssist' OnFor 20M) Or (Not 'SystemEnable') Or ('HWPLagRelease') Then
   'HWPLagAssist' = Off
End If

//If HWPLagAssist Then
//    PumpSignal = Scale (HWDp_CO, 0, 0, 0, 100, 100)
//  Else
//    PumpSignal = Scale (HWDp_CO, 0, 0, 0, 50, 100)
//End If

//Pump Failure
If 'HWP1Enable' And (Not 'HWP1Status') OnFor 'PumpFailDly_Sp':S Then
    'HWP1_Fail' = On
  Else
    'HWP1_Fail' = Off
End If

If ('HWP2Enable' And (Not 'HWP2Status')) OnFor 'PumpFailDly_Sp':S Then
    'HWP2_Fail' = On
  Else
    'HWP2_Fail' = Off
End If


Variable HWP1Manual
Variable HWP2Manual

If Not 'HWP1Enable.Priority_Array[5]' Then
    HWP1Manual = On
  ElseIf  Not 'HWP1Enable.Priority_Array[8]' Then
    HWP1Manual = On
  Else
    HWP1Manual = Off
End If
If Not 'HWP2Enable.Priority_Array[5]' Then
    HWP2Manual = On
  ElseIf  Not 'HWP2Enable.Priority_Array[8]' Then
    HWP2Manual = On
  Else
    HWP2Manual = Off
End If
If 'SystemEnable' Then
 Variable HWP1Call
 Variable HWP2Call
 If ('HWPLead' Or 'HWP2_Fail' Or HWP1Manual Or 'HWPLagAssist') And 'SystemEnable' Then
    'HWP1Enable' = On
    HWP1Call = On
 End If
 If (Not 'HWPLead') And (Not 'HWP2_Fail') And (Not HWP2Manual) And (Not 'HWPLagAssist') Then
    HWP1Call = Off
 End If

 If HWP1ramp < 1 And Not HWP1Call Then
  'HWP1Enable' = Off
 End If

 If ((Not 'HWPLead') Or 'HWP1_Fail' Or HWP2Manual Or 'HWPLagAssist') And 'SystemEnable' Then
    'HWP2Enable' = On
    HWP2Call = On
 End If

 If 'HWPLead' And (Not 'HWP1_Fail') And (Not HWP1Manual) And (Not 'HWPLagAssist') Then
    HWP2Call = Off
 End If
 If HWP2ramp < 1 And Not HWP2Call Then
  'HWP2Enable' = Off
 End If
ElseIf  'SystemEnable' OffFor 30M Then
 'HWP1Enable' = Off
 'HWP2Enable' = Off
End If

//Pump ramp sequence to provide a smooth start when
//pump is enabled. This minimizes Loop overshoot on start.
//HWP1 Demand
Variable HWP1ramp
If 'HWP1Status' And HWP1Call Then
    DoEvery 10S
      HWP1ramp = Min (100, HWP1ramp + (100 / ('HWPmpRamp_Sp' * 6)))
    End Do
   Else
    DoEvery 5S
      HWP1ramp = Max (0, HWP1ramp - (100 / ('HWPmpRamp_Sp' * 16)))
    End Do
End If

If HWP1Enable Then
   'HWP1_Speed' = (HWP1ramp * 0.01) * PumpSignal
 Else
   'HWP1_Speed' = 0
End If

//HWP2 Demand 
Variable HWP2ramp
If 'HWP2Status' And HWP2Call Then
    DoEvery 10S
      HWP2ramp = Min (100, HWP2ramp + (100 / ('HWPmpRamp_Sp' * 6)))
    End Do
  Else
    DoEvery 5S
      HWP2ramp = Max (0, HWP2ramp - (100 / ('HWPmpRamp_Sp' * 16)))
    End Do
End If

If HWP2Enable Then
   'HWP2_Speed' = (HWP2ramp * 0.01) * PumpSignal
 Else
   'HWP2_Speed' = 0
End If

//Flow interlock
Variable DpFlow

DpFlow = Switch ( DpFlow, 'HWDp', ('HWDp_Sp' * 0.3), ('HWDp_Sp' * 0.1))

If ('HWP1Status' Or 'HWP2Status') And DpFlow Then
    'PrimaryFlow' = On
  Else
    'PrimaryFlow' = Off
End If

//End of Program 