'HW_BlrDemand_PG13.Write_Priority' = 13
//System enable
If ('HW_HWSystemMode' = 1) Then
    If 'HW_Rem_BldgHeatCall' Or ('HW_OATInt1' And 'HW_Rem_BldgOccMWUCall') Or 'HW_OATInt2' Then
      'HW_SystemEnable' = On
    Else
      'HW_SystemEnable' = Off
    End If
  ElseIf  ('HW_HWSystemMode' = 2) Then
      'HW_SystemEnable' = On
  Else
      'HW_SystemEnable' = Off
End If
If 'HW_SystemEnable' OffFor 60S Then
    'HW_HWS_CO.Bias' = 1
    'HW_SystemRamp' = 0
End If

If ('OAT_Eff' < 'HW_OATInt1_Sp') Then
    DoEvery 3M
    'HW_OATInt1TmLeft' = Max (0, 'HW_OATInt1TmLeft' - 0.05)
    End Do
  Else
    'HW_OATInt1TmLeft' = 'HW_OATInt1Delay_Sp'
End If

If 'HW_OATInt1Delay_Sp' <= 0 Then
    'HW_OATInt1' = On
  Else
    'HW_OATInt1' = Off
End If

If (('OAT_Eff' < 'HW_OATInt2_Sp') And (Not 'HW_OATInt2')) OnFor 30M Then
    'HW_OATInt2' = On
End If
If (('OAT_Eff' > 'HW_OATInt2_Sp') And 'HW_OATInt2') OnFor 30M Then
    'HW_OATInt2' = Off
End If

//Boiler ramp sequence to provide a smooth start when
//HW system is enabled or when pumps have been off line.
//This minimizes boiler shock and Loop overshoot on cold starts.

If 'HW_SystemEnable' Then
    DoEvery 10S
      'HW_SystemRamp' = Min (100, 'HW_SystemRamp' + (100 / ('HW_SystemRamp_Sp' * RampMult * 6)))
    End Do
End If
IfOnce 'HW_HWHighLimit' Then
    'HW_SystemRamp' = 'HW_SystemRamp' * 0.25
End If

Variable RampMult

//Ramp delay factors the ramp delay time. If the system is warm,
//we dont need as long of a ramp.
RampMult = Scale ('HW_HWS', 0, 110, 1, 160, 0.2)


//Allows for remote manual override of control loop Demand
If 'HW_HWSDemand_Rem' >= 0 Then
    'HW_HWSDemand_Eff' = 'HW_HWSDemand_Rem'
  Else
    'HW_HWSDemand_Eff' = 'HW_HWSDemand_Loc'
End If

//Calculated HWs Demand percent. 
'HW_HWSDemand_Loc' = 'HW_HWS_CO' * 'HW_SystemRamp' * 0.01

'HW_Lead1Demand' = 'HW_SeqDemand'
'HW_SeqDemand' = 'HW_HWSDemand_Eff'