'HW_Pumps_PG13.Write_Priority' = 13

If 'HW_HWP1Status' Or 'HW_HWP2Status' Then
    'HW_HWPFlow' = On
  Else
    'HW_HWPFlow' = Off
End If

If ('HWP1Enable' And Not 'HWP1_Status') OnFor 'HW_PumpFailDly_Sp':S Then
    'HW_HWP1Fail' = On
  Else
    'HW_HWP1Fail' = Off
End If

If ('HWP2Enable' And Not 'HWP2_Status') OnFor 'HW_PumpFailDly_Sp':S Then
    'HW_HWP2Fail' = On
  Else
    'HW_HWP2Fail' = Off
End If

Variable HWP1Manual
Variable HWP2Manual

If Not 'HWP1Enable.PriorityArray[5]' Then
   HWP1Manual = On
 ElseIf  Not 'HWP1Enable.PriorityArray[8]' Then
   HWP1Manual = On
 Else
    HWP1Manual = Off
End If
If Not 'HWP2Enable.PriorityArray[5]' Then
   HWP2Manual = On
 ElseIf  Not 'HWP2Enable.PriorityArray[8]' Then
   HWP2Manual = On
 Else
    HWP2Manual = Off
End If

Variable HWP1Call
Variable HWP2Call
If 'HW_HWPCall' And (('HW_LeadPump' = 1) Or 'HW_HWP2Fail' Or HWP2Manual Or 'HW_HWPLagAssist') Then
    'HWP1Enable' = On
    HWP1Call = On
End If

If Not 'HW_HWPCall' Then
    HWP1Call = Off
 ElseIf  (Not 'HW_HWPLagAssist') And (('HW_LeadPump' = 2)) And (Not 'HW_HWP2Fail') And (Not HWP2Manual) Then
    HWP1Call = Off
End If

If 'HWP1Ramp' < 1 And Not HWP1Call Then
  'HWP1Enable' = Off
End If

If 'HW_HWPCall' And ((('HW_LeadPump' = 2)) Or 'HW_HWP1Fail' Or HWP1Manual Or 'HW_HWPLagAssist') Then
    'HWP2Enable' = On
    HWP2Call = On
End If
If Not 'HW_HWPCall' Then
    HWP2Call = Off
  ElseIf  (Not 'HW_HWPLagAssist') And ('HW_LeadPump' = 1) And (Not 'HW_HWP1Fail') And (Not HWP1Manual) Then
    HWP2Call = Off
End If

If 'HWP2Ramp' < 1 And Not HWP2Call Then
  'HWP2Enable' = Off
End If


//Pump ramp sequence to provide a smooth start when
//pump is enabled. This minimizes Loop overshoot on start.
//HWP1 Demand
If 'HWP1_Status' And HWP1Call Then
    DoEvery 10S
      'HWP1Ramp' = Min (100, 'HWP1Ramp' + (100 / ('HWPmpRamp_Sp' * 6)))
    End Do
   Else
    DoEvery 10S
      'HWP1Ramp' = Max (0, 'HWP1Ramp' - (100 / ('HWPmpRamp_Sp' * 16)))
    End Do
End If

//HWP2 Demand 
If 'HWP2_Status' And HWP2Call Then
    DoEvery 10S
      'HWP2Ramp' = Min (100, 'HWP2Ramp' + (100 / ('HWPmpRamp_Sp' * 6)))
    End Do
  Else
    DoEvery 10S
      'HWP2Ramp' = Max (0, 'HWP2Ramp' - (100 / ('HWPmpRamp_Sp' * 16)))
    End Do
End If

If 'HW_SystemEnable' OffFor 'HWPmpOffDelay_Sp':M Then
    'HW_HWPCall' = Off
  Else
    'HW_HWPCall' = On
End If

If ((Not 'HW_HWPLagAssist') And ('HWPmpRamp' > 99)) OnFor 15M Then
    'HW_HWPLagAssist' = On
End If

If ('HWPmpRamp' < 50) OnFor 15M Then
    'HW_HWPLagAssist' = Off
End If