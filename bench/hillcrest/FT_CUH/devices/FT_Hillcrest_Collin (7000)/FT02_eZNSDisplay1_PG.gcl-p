If PowerUp Then
  'LinkLCD2.Line_2' = 'FT02_Eff_ZoneTemp'
  'LinkLCD2.Line_3' = 'FT02_Zone_EffSp'
  'LinkLCD2.Line_2_Units' = 3
  'LinkLCD2.Line_3_Units' = 3
  'LinkLCD2.Occupancy' = 2
  'LinkLCD2.On' = 1
  'FT02_DNSAlgMode' = 1
End If

//Local setpoint adjust enable
//Change mode requires that button 2 be pressed 4 times (or as programmed)
//prior to being able to change stat setting. This alleviates unauthorized tampering.
IfOnce 'FT02_DNSMode' = 1 Then
    'FT02_EnableDNS' = On
    'LinkLCD2.Set' = 0
End If

IfOnce 'FT02_DNSMode' = 2 Then
    'FT02_EnableDNS' = Off
    'LinkLCD2.Set' = 0
End If

If 'FT02_DNSMode' = 3 Then
  Variable KeyCount
  IfOnce ('LinkLCD2.Key_Press' = 2) Then
    KeyCount = KeyCount + 1
  End If
  If KeyCount >= 4 Then
      'FT02_EnableDNS' = On
  End If
  If ('LinkLCD2.Key_Press' = 0) OnFor 60S Then
      'FT02_EnableDNS' = Off
      KeyCount = 0
  End If
  IfOnce 'FT02_EnableDNS' Then
    'LinkLCD2.Set' = 1
  End If
  IfOnce Not 'FT02_EnableDNS' Then
    'LinkLCD2.Set' = 0
  End If
End If
IfOnce 'FT02_DNSMode' = 2 Then
    'FT02_EnableDNS' = Off
    'LinkLCD2.Set' = 0
End If

//This updates the display temps on a periodic basis
DoEvery 1M
  'LinkLCD2.Line_2' = 'FT02_Eff_ZoneTemp'
  'LinkLCD2.Line_3' = 'FT02_Zone_EffSp'
  'LinkLCD2.Line_3_Units' = 3
End Do

//Occupancy Icons
IfOnce ('FT02_OccMode' = 3) Or ('FT02_OccMode' = 4) Then
    'LinkLCD2.Occupancy' = 2
    'LinkLCD2.On' = 1
    'LinkLCD2.Off' = 0
End If

IfOnce ('FT02_OccMode' = 1) Or ('FT02_OccMode' = 2) Then
    'LinkLCD2.Occupancy' = 1
    'LinkLCD2.On' = 0
    'LinkLCD2.Off' = 1
End If

//Heat Icon
IfOnce ('FT02_EffZoneHtg' >= 5) Then
    'LinkLCD2.Heating' = 1
End If
IfOnce ('FT02_EffZoneHtg' <= 1) Then
    'LinkLCD2.Heating' = 0
End If

//End of Program