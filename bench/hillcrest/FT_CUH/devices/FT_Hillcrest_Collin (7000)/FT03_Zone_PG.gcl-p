//Zone_PG
Variable HtCall
Variable ClCall
Variable HtRamp
Variable ClRamp

//Eff SP
If ('FT03_OccMode' = 3) Or ('FT03_OccMode' = 4) Or ('FT03_OccMode' = 5) Then //occ, bypass, warmup
    'FT03_ZoneHtg_EffSp' = Min (('FT03_Zone_EffSp' - ('FT03_ZoneDb_Sp' * 0.5)), 'FT03_OcHtgMax_Sp')
    'FT03_ZoneClg_EffSp' = Max (('FT03_Zone_EffSp' + ('FT03_ZoneDb_Sp' * 0.5)), 'FT03_OcClgMin_Sp')
  ElseIf  'FT03_OccMode' = 2 Then //standby
    'FT03_ZoneHtg_EffSp' = Min (('FT03_Zone_EffSp' - 'FT03_StandbyOffset_Sp' - ('FT03_ZoneDb_Sp' * 0.5)), 'FT03_OcHtgMax_Sp')
    'FT03_ZoneClg_EffSp' = Max (('FT03_Zone_EffSp' + 'FT03_StandbyOffset_Sp' + ('FT03_ZoneDb_Sp' * 0.5)), 'FT03_OcClgMin_Sp')
  Else //unocc
    'FT03_ZoneHtg_EffSp' = Min ('FT03_UnocHt_Sp', 'FT03_OcHtgMax_Sp')
    'FT03_ZoneClg_EffSp' = Max ('FT03_UnocCl_Sp', 'FT03_OcClgMin_Sp')
End If

//allow remote override of zone sp
If 'FT03_Zone_RemSp' > 0 Then
    'FT03_Zone_EffSp' = 'FT03_Zone_RemSp'
  Else
    'FT03_Zone_EffSp' = 'FT03_Zone_LocSp'
End If

//calculate local demand after ramp
'FT03_LocZoneHtg' = 'FT03_ZoneHtg_CO' * HtRamp * 0.01
'FT03_LocZoneClg' = 'FT03_ZoneClg_CO' * ClRamp * 0.01

//heating or cooling call and previous mode latching
If (('FT03_ZoneHtg_CO' > ('FT03_EffZoneClg' + 5)) And 'FT03_ControlMode' = 3) OnFor 'FT03_ZoneDbDelay_Sp':S Then
    HtCall = On
    'FT03_PrevControlMode' = 3
  ElseIf  (('FT03_ZoneClg_CO' > ('FT03_EffZoneHtg' + 5)) And 'FT03_ControlMode' = 3) OnFor 'FT03_ZoneDbDelay_Sp':S Then
    ClCall = On
    'FT03_PrevControlMode' = 3
  ElseIf  (('FT03_ControlMode' = 1) And ('FT03_EffZoneHtg' <= 0)) OnFor 'FT03_ZoneDbDelay_Sp':S Then
    HtCall = Off
    'FT03_PrevControlMode' = 1
  ElseIf  (('FT03_ControlMode' = 2) And ('FT03_EffZoneClg' <= 0)) OnFor 'FT03_ZoneDbDelay_Sp':S Then
    ClCall = Off
    'FT03_PrevControlMode' = 2
End If

//determine control mode
If 'FT03_RemZoneHtg' >= 0 Then //remote htg
    'FT03_EffZoneHtg' = 'FT03_RemZoneHtg'
    'FT03_EffZoneClg' = 0
    'FT03_ControlMode' = 1
  ElseIf  'FT03_RemZoneClg' >= 0 Then //remote clg
    'FT03_EffZoneClg' = 'FT03_RemZoneClg'
    'FT03_EffZoneHtg' = 0
    'FT03_ControlMode' = 2
  ElseIf  ClCall Then //auto clg
    'FT03_ControlMode' = 2
    'FT03_EffZoneHtg' = 'FT03_LocZoneHtg'
    'FT03_EffZoneClg' = 'FT03_LocZoneClg'
  ElseIf  HtCall Then
    'FT03_ControlMode' = 1 //auto htg
    'FT03_EffZoneHtg' = 'FT03_LocZoneHtg'
    'FT03_EffZoneClg' = 'FT03_LocZoneClg'
  Else
    'FT03_ControlMode' = 3 //auto deadband
    'FT03_EffZoneHtg' = 'FT03_LocZoneHtg'
    'FT03_EffZoneClg' = 'FT03_LocZoneClg'
End If

//Ramp delay
If 'FT03_ControlMode' = 1 Then
    DoEvery 10S
      HtRamp = Min (100, HtRamp + (1000 / 'FT03_ZoneHtRamp_Sp'))
    End Do
  Else
    HtRamp = 0
End If
If 'FT03_ControlMode' = 2 Then
    DoEvery 10S
      ClRamp = Min (100, ClRamp + (1000 / 'FT03_ZoneClRamp_Sp'))
    End Do
  Else
    ClRamp = 0
End If

//unocc htg demand & standby deleted bc they weren't used

//End of Program